# An example brainfuck interpreter in Knight. Note that this
# interpreter uses the native knight integer type, and not
# the traditional single byte cells.

# Read the sourcecode in from stdin, and initialize the position
# into it to zero.
; = source PROMPT
; = srcpos 0

# Setup the brainfuck VM. We just need a list of 30,000 zeros and
# an index into it, which starts at zero.
; = ary * ,0 30000
; = idx 0

# Since brainfuck reads from stdin a byte at a time, we need to
# parse 
; = inpbuf ""
; = list_indices @
; = bf BLOCK
	: WHILE < srcpos LEN source
		; = chr GET source srcpos 1
		; = srcpos + srcpos 1
		: IF (? chr '>') : = idx + idx 1
		: IF (? chr '<') : = idx - idx 1
		: IF (? chr '+') : = ary SET ary idx 1 ,(+ (GET ary idx 0) 1)
		: IF (? chr '-') : = ary SET ary idx 1 ,(- (GET ary idx 0) 1)
		: IF (? chr '.') : OUTPUT + ASCII (GET ary idx 0) "\"
		: IF (? chr ',')
			; = inpbuf | inpbuf PROMPT
			; = ary SET ary idx 1 (ASCII GET inpbuf 0 0)
			: = inpbuf SET inpbuf 0 1 ""
		: IF ? chr '['
			; = list_indices +,srcpos list_indices
			; WHILE GET ary idx 0
				; CALL bf
				: = srcpos GET list_indices 0 0
			; = srcpos endpos
			: = list_indices SET list_indices 0 1 @
		: IF ? chr ']'
			; = endpos srcpos
			: = srcpos LEN source
		: NULL
: CALL bf
